apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-android-extensions"
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'


repositories {
    google()
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    mavenCentral()
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}

android {
    Properties customProps = new Properties()
    customProps.load(new FileInputStream("../secrets/zmuzik.slidingpuzzle2.properties"))

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "zmuzik.slidingpuzzle2"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName "2.10"
        buildConfigField("String", "FLICKR_API_KEY", customProps.FLICKR_API_KEY)
        archivesBaseName = "zmuzik-slidingpuzzle2-v$versionName"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file("../../secrets/zmuzik.keystore")
            keyAlias customProps.KEY_ALIAS
            storePassword customProps.STORE_PASSWORD
            keyPassword customProps.KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.27.0-eap13'

    // androidx
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    // material design
    implementation "com.google.android.material:material:1.0.0"

    // ktx
    implementation 'androidx.core:core-ktx:1.0.0'

    // navigation
    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha06"
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0-alpha06"

    // koin
    implementation "org.koin:koin-androidx-scope:1.0.1"
    implementation "org.koin:koin-androidx-viewmodel:1.0.1"

    // networking
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"
    implementation "com.squareup.okhttp3:okhttp:3.11.0"
    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.squareup.retrofit2:converter-gson:2.4.0"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    // image loading
    implementation "com.squareup.picasso:picasso:2.5.2"

    // logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // debug analysis
    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.6.2"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.6.2"
}
