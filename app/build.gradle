apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: "kotlin-android-extensions"


repositories {
    google()
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    mavenCentral()
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}

android {
    Properties customProps = new Properties()
    customProps.load(new FileInputStream("../secrets/zmuzik.slidingpuzzle2.properties"))

    compileSdkVersion 29

    defaultConfig {
        applicationId "zmuzik.slidingpuzzle2"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode getVersionCode()
        versionName "2.12"
        buildConfigField("String", "FLICKR_API_KEY", customProps.FLICKR_API_KEY)
        archivesBaseName = "zmuzik-slidingpuzzle2-v$versionName"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file("../../secrets/zmuzik.keystore")
            keyAlias customProps.KEY_ALIAS
            storePassword customProps.STORE_PASSWORD
            keyPassword customProps.KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    // androidx
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.preference:preference-ktx:1.1.0"

    // material design
    implementation "com.google.android.material:material:1.1.0"

    // ktx
    implementation 'androidx.core:core-ktx:1.2.0'

    // navigation
    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0"
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0"

    // koin
    implementation "org.koin:koin-androidx-scope:1.0.1"
    implementation "org.koin:koin-androidx-viewmodel:1.0.1"

    // networking
    implementation "com.squareup.okhttp3:logging-interceptor:3.14.4"
    implementation "com.squareup.okhttp3:okhttp:3.14.4"
    implementation "com.squareup.retrofit2:retrofit:2.7.1"
    implementation "com.squareup.retrofit2:converter-gson:2.7.1"

    // image loading
    implementation "com.squareup.picasso:picasso:2.5.2"

    // logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // debug analysis
    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.6.3"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.6.3"
}
